#!ipxe

echo "rstms netboot menu v0.0.6"

#set cmdline console=ttyS1,115200n8
dhcp || echo "dhcp failed"

:main_menu
clear main_menu_choice
menu Reliance Systems Netboot Menu
item --gap Default:
item mac_autoinstall MAC Address Autoinstall
item local Boot from local hdd
item --gap netboot.rstms.net
item rstms_openbsd75 OpenBSD 7.5
item rstms_debian12 Debian 12 bookworm
item --gap netboot.xyz
item netboot_xyz netboot.xyz menu
item --gap Tools:
item shell iPXE shell
choose main_menu_choice || goto menu_exit
echo ${cls}
goto ${main_menu_choice}
goto main_menu


:error
echo Error occured, press any key to return to menu ...
prompt
goto main_menu

:mac_autoinstall
chain --autofree http://netboot.rstms.net/${net0/mac}.ipxe || error

:rstms_openbsd75
chain --autofree http://netboot.rstms.net/rstms-netboot-openbsd.ipxe || error
goto error

:rstms_debian12
chain --autofree http://netboot.rstms.net/rstms-netboot-debian.ipxe || error
goto error

:netboot_xyz
chain --autofree https://boot.netboot.xyz
goto error

:local
echo Booting from local disks ...
exit 1

:shell
echo Type "exit" to return to menu.
shell
goto main_menu

:menu_exit
exit
